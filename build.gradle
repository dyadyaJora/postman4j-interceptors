plugins {
    id 'java'
    id 'application'
    id 'signing'
    alias(libs.plugins.com.netflix.nebula.javadoc.jar)
    alias(libs.plugins.com.netflix.nebula.source.jar)
    alias(libs.plugins.com.netflix.nebula.maven.publish)
    alias(libs.plugins.com.netflix.nebula.maven.developer)
    alias(libs.plugins.com.netflix.nebula.contacts)
    alias(libs.plugins.org.jreleaser)
    alias(libs.plugins.com.github.ben.manes.versions)
}

group = 'dev.jora.postman4j'

repositories {
    mavenCentral()
    gradlePluginPortal()
}


subprojects {
    apply plugin: 'java'

    if (project.name == 'postman4j-models' || project.name == 'hc5-interceptor'|| project.name == 'spring-interceptor' || project.name == 'restassured-interceptor') {
        apply plugin: 'com.netflix.nebula.contacts'
        apply plugin: 'com.netflix.nebula.javadoc-jar'
        apply plugin: 'com.netflix.nebula.source-jar'
        apply plugin: 'com.netflix.nebula.maven-publish'
        apply plugin: 'com.netflix.nebula.maven-developer'
        apply plugin: 'org.jreleaser'
        apply plugin: 'application'
        apply plugin: 'signing'

        ext {
            stagingDirName = 'staging-deploy'
            localDirName = 'staging-deploy2'
        }

        publishing {
            publications {
                nebula(MavenPublication) {
                    pom {
                        url.set(findProperty('projectRepoUrl'))

                        licenses {
                            license {
                                name.set("The MIT License")
                                url.set("https://opensource.org/licenses/MIT")
                                distribution.set("repo")
                            }
                        }


                        scm {
                            connection = 'scm:git:git://github.com/dyadyaJora/postman4j-interceptors.git'
                            developerConnection = 'scm:git:ssh://github.com:dyadyaJora/postman4j-interceptors.git'
                            url = 'https://github.com/dyadyaJora/postman4j-interceptors'
                        }
                    }
                }
            }

            repositories {
                maven {
                    name = "Staging"
                    url = layout.buildDirectory.dir(stagingDirName)
                }
                maven {
                    name = "LocalMavenWithChecksums"
                    url = uri(layout.buildDirectory.dir(localDirName))
                }
//                maven {
//                    name = 'Snapshots'
//                    url = 'https://central.sonatype.com/repository/maven-snapshots/'
//                    credentials {
//                        username = System.getenv("MAVEN_USERNAME")
//                        password = System.getenv("MAVEN_PASSWORD")
//                    }
//                }
            }
        }


        signing {
            required = false
            sign publishing.publications.nebula
        }

    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenCentral()
    }
    dependencies {
        annotationProcessor libs.lombok
        implementation libs.lombok

        testImplementation platform(Testing.junit.bom)
        testImplementation Testing.junit.jupiter
    }

    test {
        useJUnitPlatform()
    }
}


contacts {
    'idyadyajora@gmail.com' {
        moniker 'George Kindra'
        github 'dyadyaJora'
    }
}

def releasedSubProject = findProperty('releasedSubProject') ?: '.'
def releaseMaxRetries = Integer.valueOf(findProperty('releaseMaxRetries') ? findProperty('releaseMaxRetries').toString() : '60')

jreleaser {
    gitRootSearch = true
    project {
        copyright = 'jora.dev'
        description = 'Postman collection models for postman4j.'
        snapshot {
            enabled = true
        }
    }
    signing {
        active = 'ALWAYS'
        armored = true
        mode = 'FILE'
        publicKey = 'public.key'
        secretKey = 'private.key'
    }
    deploy {
        maven {
            active = 'ALWAYS'
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    maxRetries = releaseMaxRetries
                    stagingRepository("${releasedSubProject}/build/staging-deploy")

                }
            }
        }
    }
    release {
        github {
            enabled = false
        }
    }
}
